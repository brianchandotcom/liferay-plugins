String userDir = System.getProperty("user.dir");

def antProps = new Properties()

antProps.load(new FileInputStream("build.properties"))

if (!hasProperty("lfrPluginsExcludes"))
{
	ext.lfrPluginsExcludes = antProps["plugins.excludes"]
}

if (!hasProperty("lfrPluginsIncludes"))
{
	ext.lfrPluginsIncludes = antProps["plugins.includes"]
}

gradle.ext.compileDeps = [];

ext.includeTransitive = { String[] projectPaths ->
	include projectPaths;

	for (String projectPath : projectPaths) {
		String projectSettings;

		projectSettings = projectPath.replaceAll(":", "/");
		projectSettings = projectSettings + "/dependency-settings.gradle";
		projectSettings = projectSettings.replaceFirst("^/", "");
		projectSettings = rootDir.toString() + "/" + projectSettings;

		File projectSettingsFile = new File(projectSettings);

		if (projectSettingsFile.exists() && projectSettingsFile.length() > 0) {
			apply from: projectSettingsFile;

			String projectCompileDeps = projectSettingsFile.text;
			
			projectCompileDeps =
				projectCompileDeps.replaceAll("includeTransitive", "");
			projectCompileDeps = projectCompileDeps.replaceAll(" ", "");
			projectCompileDeps = projectCompileDeps.replaceAll('"', "");
			projectCompileDeps = projectCompileDeps.replaceAll("\\s\$", "");
			
			String[] projectCompileDepsList = projectCompileDeps.split("\\n");

			gradle.compileDeps <<
				[projectPath: projectPath, compile: projectCompileDepsList];
		}
	}
}

FileTree fileTree = fileTree(userDir) {
	if ((lfrPluginsIncludes != "") && (lfrPluginsIncludes != "*")) {
		lfrPluginsIncludes = lfrPluginsIncludes.replaceAll(" ", "");
		lfrPluginsIncludes = lfrPluginsIncludes.replaceAll(",+", ",");

		def lfrPluginsIncludesArray = lfrPluginsIncludes.split(",");

		lfrPluginsIncludesArray = lfrPluginsIncludesArray.collect(
			{
				"**/" + it + "/build.gradle"
			}
		);
 
		lfrPluginsIncludesArray.each(
			{
				include(it);
			}
		);
	}
	else {
		include("**/build.gradle");
	}
 
	if ((lfrPluginsExcludes != "") && (lfrPluginsExcludes != "*")) {
		lfrPluginsExcludes = lfrPluginsExcludes.replaceAll(" ", "");
		lfrPluginsExcludes = lfrPluginsExcludes.replaceAll(",+", ",");

		String[] lfrPluginsExcludesArray = lfrPluginsExcludes.split(",");

		lfrPluginsExcludesArray = lfrPluginsExcludesArray.collect(
			{
				"**/" + it + "/build.gradle"
			}
		);
 
		lfrPluginsExcludesArray.each(
			{
				exclude(it);
			}
		);
	}
	else {
		exclude("**/build.gradle");
	}

	exclude("build.gradle");
}

fileTree.each(
	{
		URI rootURI = rootDir.toURI();

		URI pluginURI = it.toURI();

		pluginURI = rootURI.relativize(pluginURI);

		String pluginDir = pluginURI.toString();

		pluginDir = pluginDir.replaceFirst("/build.gradle", "");

		String newProject = ":" + pluginDir.replaceAll("[\\/]", ":");

		includeTransitive(newProject);
	}
);

include(":shared:portal-compat-shared");